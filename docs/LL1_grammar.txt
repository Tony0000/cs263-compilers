PROGRAM -> program id semicolon
 LIBRARIES
 DECLRS
 SUB_PGM_DECLRS
 COMPOUND_STMT dot .

LIBRARIES -> uses ID_LIST semicolon
| .

ID_LIST -> id ID_LISTR .

ID_LISTR -> comma id ID_LISTR
| .

DECLRS -> var VAR_DECLR DECLRSR |
const CONST_DECLR DECLRSR
| type NEW_TYPE DECLRSR
| .

VAR_DECLR -> ID_LIST double_dot TYPE semicolon VAR_DECLR
| .

CONST_DECLR -> id assignop CONST_VALUES semicolon CONST_DECLR | .

CONST_VALUES -> const_string | const_char | const_int | const_real | const_boolean .

TYPE -> STD_TYPE
| array open_bracket CTRL_VALUE double_dot CTRL_VALUE close_bracket of STD_TYPE .

STD_TYPE -> integer | real | string | boolean | char .

NEW_TYPE -> id assignop TYPE semicolon NEW_TYPE .

NEW_TYPER -> semicolon NEW_TYPE | .

SUB_PGM_DECLRS -> SUB_PGM_DECLR semicolon SUB_PGM_DECLRS
| .

SUB_PGM_DECLR -> SUB_PGM_HEAD DECLRS COMPOUND_STMT .

SUB_PGM_HEAD -> function SUB_PGM_HEAD_PARAM declr STD_TYPE semicolon
| procedure SUB_PGM_HEAD_PARAM semicolon .

SUB_PGM_HEAD_PARAM -> id open_paren PARAM_LIST close_paren .

PARAM_LIST -> ID_LIST double_dot TYPE PARAM_LISTR
| .

PARAM_LISTR -> semicolon ID_LIST double_dot TYPE PARAM_LIST
| .

COMPOUND_STMT -> begin OPT_STMT end .

OPT_STMT -> STMT_LIST | .

STMT_LIST -> STMT STMT_LISTR .

STMT_LISTR -> semicolon STMT STMT_LISTR
| .

STMT -> COMPOUND_STMT
| PROCEDURE_STMT
| if EXPRESSION_LIST then STMT OPT_CONDITIONAL_STMT
| while EXPRESSION_LIST do STMT
| for id assignop CTRL_VALUE to CTRL_VALUE OPT_FOR_STEP do COMPOUND_STMT .

CTRL_VALUE -> id | const_int .

OPT_CONDITIONAL_STMT -> else STMT | .

OPT_FOR_STEP -> step CTRL_VALUE | .

PROCEDURE_STMT -> read open_paren ID_LIST close_paren
| write open_paren WRITE_PARAM_LIST
| writeln open_paren WRITE_PARAM_LIST
| id ASSIGN_OR_SUB_PGM_CALL .

WRITE_PARAM_LIST -> const_string FIELD_WIDTH WRITE_PARAM_LISTR
| VARIABLE FIELD_WIDTH WRITE_PARAM_LISTR .

WRITE_PARAM_LISTR -> comma WRITE_PARAM_LIST
| close_paren .

FIELD_WIDTH -> double_dot const_int PRECISION | .

PRECISION -> double_dot const_int | .

ASSIGN_OR_SUB_PGM_CALL -> open_paren ID_LIST close_paren
| INDEX assignop EXPRESSION .

VARIABLE -> id INDEX.

INDEX -> open_bracket SIMPLE_EXPRESSION close_bracket
| .

VARIABLE_LIST -> VARIABLE VARIABLE_LISTR .

VARIABLE_LISTR -> comma VARIABLE_LIST | .

EXPRESSION_LIST ->  EXPRESSION EXPRESSION_LISTR .

EXPRESSION_LISTR ->  logicop EXPRESSION_LIST | .

EXPRESSION -> SIMPLE_EXPRESSION EXPRESSIONR .

EXPRESSIONR -> relop SIMPLE_EXPRESSION
| .

SIMPLE_EXPRESSION -> TERM SIMPLE_EXPRESSIONR
| SIGN TERM SIMPLE_EXPRESSIONR .

SIMPLE_EXPRESSIONR -> addop SIMPLE_EXPRESSION | .

TERM -> FACTOR TERMR .

TERMR -> mulop FACTOR TERMR | .

FACTOR -> VARIABLE
| const_int
| const_real
| not FACTOR .

SIGN -> plus
| minus .
